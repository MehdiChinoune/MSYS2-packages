# Maintainer: Ray Donnelly <mingwandroid@gmail.com>

pkgname=pacman-mirrors
pkgver=20221210
pkgrel=1
pkgdesc="MSYS2 mirror list for use by pacman"
arch=('any')
url="https://www.msys2.org/dev/mirrors/"
license=('GPL')
source=(mirrorlist.msys
        mirrorlist.mingw
        mirrorlist.msys-x86_64-v3
        mirrorlist.mingw-x86_64-v3)
sha256sums=('f105e99b174b409dbcec37d0e5e7473b22092cdbf5cd52ef9c5943f62b955017'
            '9bf93b4feeddcfc788fee17965dc6339f91d4fc3059812a9b3bc349e6bde0065'
            '3786b0d636231edece2f304324683589cf51ac7cef81978774eb61022781d37b'
            'e9e1ee9d84a324379e854a0bad8324c2145585592a718fe897c87b62e369c269')
backup=(
  'etc/pacman.d/mirrorlist.msys'
  'etc/pacman.d/mirrorlist.mingw'
  'etc/pacman.d/mirrorlist.mingw64'
  'etc/pacman.d/mirrorlist.ucrt64'
  'etc/pacman.d/mirrorlist.clang64'
  'etc/pacman.d/mirrorlist.msys-x86_64-v3'
  'etc/pacman.d/mirrorlist.mingw-x86_64-v3'
)
package() {
  mkdir -p ${pkgdir}/etc/pacman.d
  install -m644 ${srcdir}/mirrorlist.msys ${pkgdir}/etc/pacman.d/
  install -m644 ${srcdir}/mirrorlist.mingw ${pkgdir}/etc/pacman.d/
  # x86_64-v3
  install -m644 ${srcdir}/mirrorlist.msys-x86_64-v3 ${pkgdir}/etc/pacman.d/
  install -m644 ${srcdir}/mirrorlist.mingw-x86_64-v3 ${pkgdir}/etc/pacman.d/

  # For backwards compatibility
  install -m644 ${srcdir}/mirrorlist.mingw ${pkgdir}/etc/pacman.d/mirrorlist.mingw64
  sed -s 's|$repo|x86_64|g' -i ${pkgdir}/etc/pacman.d/mirrorlist.mingw64
  install -m644 ${srcdir}/mirrorlist.mingw ${pkgdir}/etc/pacman.d/mirrorlist.ucrt64
  sed -s 's|$repo|ucrt64|g' -i ${pkgdir}/etc/pacman.d/mirrorlist.ucrt64
  install -m644 ${srcdir}/mirrorlist.mingw ${pkgdir}/etc/pacman.d/mirrorlist.clang64
  sed -s 's|$repo|clang64|g' -i ${pkgdir}/etc/pacman.d/mirrorlist.clang64
}
