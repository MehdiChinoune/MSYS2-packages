# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=z3
pkgbase=${_realname}
pkgname=("${_realname}")
pkgver=4.14.0
pkgrel=1
pkgdesc="Z3 is a high-performance theorem prover being developed at Microsoft Research."
arch=('x86_64')
url="https://github.com/Z3Prover/z3"
license=('spdx:MIT')
depends=("gcc-libs"
         "gmp")
makedepends=("gcc"
             "cmake"
             "ninja"
             "python"
             "gmp-devel")
source=("https://github.com/Z3Prover/z3/archive/z3-${pkgver}.tar.gz")
sha256sums=('63430c3aab76f75b1d2c53177f94351caeca26e218f4cc060a1fc029059af683')

build() {
  local -a extra_config
  if check_option "debug" "y"; then
    extra_config+=(-DCMAKE_BUILD_TYPE=Debug)
  else
    extra_config+=(-DCMAKE_BUILD_TYPE=Release)
  fi

  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  cmake \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DZ3_BUILD_LIBZ3_SHARED=ON \
    -DZ3_USE_LIB_GMP=ON \
    -DZ3_BUILD_PYTHON_BINDINGS=OFF \
    -DZ3_BUILD_TEST_EXECUTABLES=OFF \
    -DZ3_INCLUDE_GIT_HASH=OFF \
    -DZ3_INCLUDE_GIT_DESCRIBE=OFF \
    "${extra_config[@]}" \
    ../${_realname}-${_realname}-${pkgver}

  cmake --build .
}

check() {
 cd "${srcdir}"/build-${MSYSTEM}
 ctest test-z3 || true
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  DESTDIR="${pkgdir}" cmake --install .

  install -Dm644 "${srcdir}"/${_realname}-${_realname}-${pkgver}/LICENSE.txt \
    "${pkgdir}"/usr/share/licenses/${_realname}/LICENSE
}
